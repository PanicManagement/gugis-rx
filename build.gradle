plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'signing'
    id 'com.github.hierynomus.license' version '0.11.0'
}

group = 'com.github.lukaszbudnik.gugis-rx'
archivesBaseName = 'gugis-rx'
version = '0.5'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenCentral()

    maven {
        url 'http://repository.apache.org/content/repositories/snapshots'
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.1'
    compile 'org.apache.logging.log4j:log4j-core:2.1'
    compile 'org.projectlombok:lombok:1.14.8'
    compile 'com.google.inject:guice:3.0'
    compile 'com.google.inject.extensions:guice-multibindings:3.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.netflix.rxjava:rxjava-core:0.20.7'
    compile 'commons-beanutils:commons-beanutils:1.9.2'

    testCompile 'org.apache.onami:org.apache.onami.test:1.4.0-incubating'
    testCompile 'junit:junit:4.12'
}

license {
    header project.file('HEADER')

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Łukasz Budnik'
    ext.email = 'lukasz.budnik@gmail.com'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task printTestErrors {
    def xmlReports = new File("${buildDir}/test-results")
    if (xmlReports.exists()) {
        xmlReports.eachFileMatch(groovy.io.FileType.FILES, ~/TEST\-.*\.xml/) {
            def testsuite = new groovy.util.XmlParser().parse(it)
            testsuite.testcase.findAll {
                it.failure.size() > 0
            }.each {
                println it
            }
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def deployUsername = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('DEPLOY_USERNAME')

def deployPassword = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('DEPLOY_PASSWORD')


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            pom.project {
                name 'gugis-rx'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Guice and RxJava-based lightweight and robust framework for creating composite components.'
                url 'https://github.com/lukaszbudnik/gugis-rx'

                scm {
                    connection 'scm:git:git@github.com:lukaszbudnik/gugis-rx.git'
                    developerConnection 'scm:git:git@github.com:lukaszbudnik/gugis-rx.git'
                    url 'git@github.com:lukaszbudnik/gugis-rx.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lukaszbudnik'
                        name 'Łukasz Budnik'
                        email 'lukasz.budnik@gmail.com'
                    }
                }
            }
        }
    }
}
